/* will produce a fat jar containing all html/js dist in public_html/apps*/

plugins {
    id "java"
    id "com.github.node-gradle.node" version "7.1.0"
}

java {
    targetCompatibility = rootProject.ext.javaVersion
    sourceCompatibility = rootProject.ext.javaVersion
}

def isCI = System.getenv('CI') ? true : false


node {
    version = '20.19.6'
    npmVersion = "9.8.1"
    download = !isCI
}

npm_install {
    inputs.files(fileTree('.') {
        include('**/package.json','**/package-lock.json')
        exclude("**/node_modules/**")
    })
    outputs.file("package-lock.json")
    environment = [
            'npm_config_optional': 'true'
    ]
}

def excludePatterns = [
        "node_modules/**", "**/node_modules/**", "**/dist/**", "**/.git/**", "**/.gradle/**", "**/build/**", "**/.nuxt/**",
        "**/public/_nuxt-styles/**", "**/.output/**", "**/showCase/**", ".turbo/**"
]

tasks.register('npm_build', NpmTask) {
    dependsOn npm_install
    args = ['run', 'build']

    // Inputs = all frontend sources
    inputs.files(fileTree(".") {
        include("**/*")
        exclude(excludePatterns)
    })
    inputs.file("package.json")
    inputs.file("package-lock.json")

    // Outputs = dist directory
    outputs.files(fileTree(".") {
        include("*/dist/**")
    })
}

// Util task to clean up all apps files created during build
tasks.register("cleanApps", Delete) {
    delete(project.layout.buildDirectory)
    def dirsToClean = ["**/dist", "**/showCase", "**/node_modules", "**/.nuxt", "**/.output", "**/.turbo","**/build","**/.gradle"]
    dirsToClean.each { pattern ->
        delete fileTree(dir: project.rootDir, includes: [pattern])
    }
    outputs.upToDateWhen { false }
}
clean.dependsOn cleanApps;

// Custom task to collect all dist folders into the resources directory
// Need because gradle cannot properly detect dynamic folders created by npm workspaces
abstract class CollectDistTask extends DefaultTask {
    @TaskAction
    void collect() {
        File targetDir = project.layout.buildDirectory
                .dir("generated/resources/public_html/apps")
                .get()
                .asFile

        project.copy { spec ->
            project.file(".").eachDir { dir ->
                def showCaseDir = new File(dir, "showCase")
                def distDir = new File(dir, "dist")
                if (showCaseDir.exists()) {
                    spec.from(showCaseDir) {
                        into(dir.name)
                    }
                }
                else if (distDir.exists()) {
                    spec.from(distDir) {
                        into(dir.name)
                    }
                }

            }
            spec.into(targetDir)
        }

        println "âœ… Collected dist folders into $targetDir"
    }
}

tasks.register("collectDist", CollectDistTask) {
    inputs.files(fileTree(".") {
        include("**/dist/**")
        exclude(excludePatterns)
    })
    outputs.dir(layout.buildDirectory.dir("generated/resources/public_html/apps"))
    dependsOn("npm_build")
}

tasks.register('format', NpmTask) {
    dependsOn npm_install
    if (!isCI) {
        args = ['run', 'format']
    } else {
        args = ['run', 'checkFormat']
    }
    inputs.files(fileTree(".") {
        include("**/*")
        exclude(excludePatterns)
    })
    outputs.files(fileTree(".") {
        include("**/*")
        exclude(excludePatterns)
    })
}

tasks.register('lint', NpmTask) {
    dependsOn npm_build
    args = ['run', 'lint']
    inputs.files(fileTree(".") {
        include("**/*")
        exclude(excludePatterns)
    })
    outputs.files(fileTree(".") {
        include("**/*")
        exclude(excludePatterns)
    })
}

task testJavaScript(type: NpmTask) {
    dependsOn npm_build

    args = ['run', 'test-ci']
    inputs.files(fileTree(".") {
        include("**/*")
        exclude(excludePatterns)
    })

}
test.dependsOn testJavaScript

sourceSets {
    main {
        resources {
            srcDir "${buildDir}/generated/main/resources"
        }
    }
}

// Link the app build to the java jar
tasks.named("processResources") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn("collectDist")

    from(layout.buildDirectory.dir("generated/resources/public_html/apps")) {
        into("public_html/apps")
    }
}

apply plugin: 'com.github.node-gradle.node'
/* will produce a fat jar containing all html/js dist in public_html/apps*/

plugins {
    id "base"
    id "com.github.node-gradle.node" version "3.1.1"
}

def nodeSpec = {
    version = '16.13.0'
    yarnVersion = '1.22.15'
    npmVersion = "8.1.0"
    download = true
}

node {
    with nodeSpec
}

subprojects {
    apply plugin: "com.github.node-gradle.node"

    task installNuxt(type: YarnTask, dependsOn: parent.yarn_install) {
        if(project.name == 'nuxt-ssr') {
            args = ['run', 'install-nuxt']
        }
    }

    //all depends on styleguide, so that needs build first
    task build(type: YarnTask, dependsOn: installNuxt) {
        node {
            with nodeSpec
        }

        if(project.name == 'nuxt-ssr') {
            inputs.files(fileTree('components'))
            inputs.files(fileTree('middleware'))
            inputs.files(fileTree('pages'))
            inputs.files(fileTree('static'))
            inputs.files(fileTree('store'))
            inputs.file('nuxt.config.js')
            outputs.dir('.nuxt')

        } else if(project.name == 'components') {
            inputs.files(fileTree('src'))
            inputs.files(fileTree('public'))
            inputs.files(fileTree('lib'))
            inputs.file('package.json')
            inputs.file('vite.config.js')
            outputs.dir('dist')
        } else {
            inputs.files(fileTree('src'))
            inputs.files(fileTree('public'))
            inputs.file('package.json')
        }

        if (project.name != 'docs' && project.name != 'nuxt-ssr' && project.name != 'components' ) {
            inputs.files(fileTree('../styleguide/src'))
            inputs.files(fileTree('../components/src'))
            inputs.file('vue.config.js')
            outputs.dir('dist')
        }

        // tell gradle to apply the build cache
        outputs.cacheIf { true }

        //depend on styleguide to build first
        if (project.name != 'styleguide') {
            dependsOn ":apps:styleguide:build"
        }

        //first install the workspace
        args = ['run', 'build']

        //remove the target folder with copy of all build materials
        delete "${project.parent.buildDir}/resources/main/public_html/apps/${project.name}"
    }

    //copy styleguide or normal app
    if (project.name == 'styleguide') {
        task buildStyleguidist(type: YarnTask, dependsOn: parent.yarn_install) {
            node {
                with nodeSpec
            }

            inputs.files(fileTree('src'))
            inputs.files(fileTree('public'))
            inputs.file('package.json')
            inputs.file('styleguide.config.js')
            inputs.file('vue.config.js')
            outputs.dir('build')

            // tell gradle to apply the build cache
            outputs.cacheIf { true }

            //first install the workspace
            args = ['run', 'styleguide:build']
        }
        task copyDistFiles(type: Copy, dependsOn: buildStyleguidist) {
            from "build"
            inputs.dir("build")
            into "${project.parent.buildDir}/resources/main/public_html/apps/${project.name}"
            outputs.dir("${project.parent.buildDir}/resources/main/public_html/apps/${project.name}")
            outputs.cacheIf { true }

        }
    } else {
        task copyDistFiles(type: Copy, dependsOn: build) {
            from "dist"
            inputs.dir("dist")
            into "${project.parent.buildDir}/resources/main/public_html/apps/${project.name}"
            outputs.dir("${project.parent.buildDir}/resources/main/public_html/apps/${project.name}")
            outputs.cacheIf { true }
        }
    }

    clean.doLast {
        project.delete(files("dist"))
        project.delete(files("build"))
        project.delete(files("node_modules"))
        if(project.name == 'nuxt-ssr') {
            project.delete(files(".nuxt"))
        }
    }

    // TODO QC and test coverage on javascript parts
//    sonarqube {
//        properties {
//            property 'sonar.sources', 'src'
//        }
//    }
}

/** skip yarn install unless changes => removed because leads to travis errors*/
//yarn_install {
////    inputs.file('package.json')
////    inputs.file('yarn.lock')
////    outputs.dir('node_modules')
////    subprojects.each {
////        inputs.file('package.json')
////        outputs.dir('node_modules')
////    }
//
//    //outputs.cacheIf { true }
//}

/* convert build files into one apps.jar that we can use as a dependency in the java part */
task createJar(type: Zip, dependsOn: subprojects.copyDistFiles) {
    baseName 'molgenis-emx2-apps'
    extension 'jar'
    destinationDir file("${buildDir}/build/libs")
    from('build/resources/main')
}

configurations {
    appResources
}
configurations.default.extendsFrom(configurations.appResources)

/* expose as artifact that can be used as dependency by other modules*/
artifacts {
    appResources(createJar.getArchiveFile()) {
        builtBy createJar
        type "jar"
    }
}



/* will produce a fat jar containing all html/js dist in public_html/apps*/

plugins {
    id "base"
    id "com.github.node-gradle.node" version "3.2.1"
}

def nodeSpec = {
    version = '16.13.0'
    yarnVersion = '1.22.15'
    npmVersion = "8.1.0"
    download = true
}

node {
    with nodeSpec
}

subprojects {
    apply plugin: "com.github.node-gradle.node"

    task build(type: YarnTask, dependsOn: parent.yarn_install) {
        node { with nodeSpec }
        // force build order: molgenis-components > styleguide > ...apps
        if (project.name != 'styleguide' && project.name != 'molgenis-components') {
            dependsOn ":apps:styleguide:build"
        }
        if (project.name == 'styleguide') {
            dependsOn ":apps:molgenis-components:build"
        }

        // set input out out put for each app type
        if (project.name == 'styleguide') {
            inputs.file('package.json')
            inputs.files(fileTree('src'))
            inputs.files(fileTree('public'))
            inputs.file('styleguide.config.js')
            inputs.file('vue.config.js')
            outputs.dir('build')
        } else if (project.name == 'molgenis-components') {
            inputs.file('package.json')
            inputs.files(fileTree('src'))
            inputs.files(fileTree('public'))
            inputs.files(fileTree('lib'))
            inputs.file('vite.config.js')
            outputs.dir('dist')
        } else if (project.name == 'nuxt-ssr') {
            inputs.file('package.json')
            inputs.files(fileTree('molgenis-components'))
            inputs.files(fileTree('middleware'))
            inputs.files(fileTree('pages'))
            inputs.files(fileTree('static'))
            inputs.files(fileTree('store'))
            inputs.file('nuxt.config.js')
            outputs.dir('.nuxt')
        } else {
            inputs.file('package.json')
            inputs.files(fileTree('src'))
            inputs.files(fileTree('public'))
            outputs.dir('dist')
        }

        outputs.cacheIf { true }

        args = ['run', 'build']

        delete "${project.parent.buildDir}/resources/main/public_html/apps/${project.name}"
    }

    if (project.name == 'styleguide') {
        task buildStyleguidist(type: YarnTask, dependsOn: build) {
            node { with nodeSpec }
            inputs.files(fileTree('src'))
            inputs.files(fileTree('public'))
            inputs.file('package.json')
            inputs.file('styleguide.config.js')
            inputs.file('vue.config.js')
            outputs.dir('build')
            outputs.cacheIf { true }
            args = ['run', 'styleguide:build']
        }
    }

    //can run in parallel with buildStyleguidist
    if (project.name == 'molgenis-components') {
        task buildComponentsShowcase(type: YarnTask, dependsOn: build) {
            node { with nodeSpec }
            inputs.file('package.json')
            inputs.files(fileTree('src'))
            inputs.files(fileTree('public'))
            inputs.files(fileTree('lib'))
            inputs.file('vite.config.js')
            outputs.dir('showCase')
            outputs.cacheIf { true }
            args = ['run', 'build-showcase']
        }
    }

    //depend on both buildComponents and buildStylegist to complete
    task copyDistFiles(type: Copy) {
        if (project.name == 'styleguide') {
            dependsOn buildStyleguidist
            from "build"
            inputs.dir("build")
        } else if (project.name == 'molgenis-components') {
            dependsOn buildComponentsShowcase
            from "showCase"
            inputs.dir("showCase")
        } else {
            from "dist"
            inputs.dir("dist")
        }

        into "${project.parent.buildDir}/resources/main/public_html/apps/${project.name}"
        outputs.dir("${project.parent.buildDir}/resources/main/public_html/apps/${project.name}")
        outputs.cacheIf { true }
    }

    clean.doLast {
        project.delete(files("dist"))
        project.delete(files("build"))
        project.delete(files("node_modules"))
        project.delete(files(".nuxt"))
        project.delete(files("showCase"))
    }

}

/* convert build files into one apps.jar that we can use as a dependency in the java part */
task createJar(type: Zip, dependsOn: subprojects.copyDistFiles) {
    archiveBaseName = 'molgenis-emx2-apps'
    archiveExtension = 'jar'
    destinationDirectory = file("${buildDir}/build/libs")
    from('build/resources/main')
}

configurations {
    appResources
}
configurations.default.extendsFrom(configurations.appResources)

/* expose as artifact that can be used as dependency by other modules*/
artifacts {
    appResources(createJar.getArchiveFile()) {
        builtBy createJar
        type "jar"
    }
}



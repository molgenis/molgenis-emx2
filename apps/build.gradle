/* will produce a fat jar containing all html/js dist in public_html/apps*/

plugins {
    id "java"
    id "com.github.node-gradle.node" version "7.0.2"
}

java {
    targetCompatibility = rootProject.ext.javaVersion
    sourceCompatibility = rootProject.ext.javaVersion
}

def isCI = System.getenv('CI') ? true : false

def nodeSpec = {
    version = '20.11.0'
    pnpmVersion = "10.10.0"
    npmVersion = "9.5.1"
    download = !isCI
}

def excludeDef = {
    // Exclude 'build', 'dist', and 'showCase' directories from inputs
    exclude ".gradle"
    exclude "dist"
    exclude "node_modules"
    exclude "showCase"
    exclude ".output"
    exclude ".fingerprint.js"
    exclude ".nuxt"
    exclude "public/_nuxt-styles"
    exclude "build"
}

node {
    with nodeSpec
}

task installJavascript(type: PnpmTask, dependsOn: pnpmSetup) {
    inputs.files(
        fileTree("${projectDir}/**/package.json"),
        file("${projectDir}/pnpm-lock.yaml"),
        file("${projectDir}/pnpm-workspace.yaml")
    );
    outputs.files(file("${projectDir}/pnpm-lock.yaml"))
    args = ['install']
}

subprojects { subproject ->
    apply plugin: 'com.github.node-gradle.node'

    node {
        workDir = file("${parent.projectDir}/.gradle/nodejs")
        npmWorkDir = file("${parent.projectDir}/.gradle/npm")
        pnpmWorkDir = file("${parent.projectDir}/.gradle/pnpm")
        download = false
    }
    nodeSetup.dependsOn parent.nodeSetup
    npmSetup.dependsOn parent.npmSetup
    pnpmSetup.dependsOn parent.pnpmSetup

    task format(type: PnpmTask, dependsOn: parent.installJavascript) {
        if (!isCI) {
            args = ['run', 'format']
            inputs.files fileTree(projectDir).matching {
                with excludeDef
            }
            outputs.files fileTree(projectDir).matching {
                with excludeDef
            }
        } else {
            args = ['run', 'checkFormat']
            inputs.files fileTree(projectDir).matching {
                with excludeDef
            }
        }
    }

    def packageJson = file("package.json").text

    task buildJavascript(type: PnpmTask, dependsOn: format) {
        environment = ["NODE_OPTIONS": "--max-old-space-size=4096"]

        //automatically get the internal dependencies
        (packageJson =~ /"([^"]+)":\s*"workspace:\*"/).each { match ->
            dependsOn ":apps:${match[1]}:buildJavascript";
        }

        //run the build
        def command =   packageJson.contains("\"name\": \"ui\"") ? "generate" : "build"
        args = ['run', command]
        doLast {
            copy {
                from packageJson.contains("\"name\": \"ui\"") ? ".output/public" : "dist"
                into "${parent.buildDir}/generated/main/resources/public_html/apps/" + project.name
            }
        }
        outputs.dir "dist"
//        for caching
        inputs.files fileTree(projectDir).matching {
            with excludeDef
        }
        parent.tasks.processResources.dependsOn buildJavascript
    }

    task lint(type: PnpmTask, dependsOn: buildJavascript) {
        if (
                packageJson.contains("\"name\": \"tailwind-components\"") ||
                        packageJson.contains("\"name\": \"ui\"") ||
                        packageJson.contains("\"name\": \"catalogue\"")
        ) {
            args = ['run', 'lint']
            inputs.files fileTree(projectDir).matching {
                with excludeDef
            }
        }
    }

    parent.tasks.processResources.dependsOn buildJavascript

    task testJavaScript(type: PnpmTask, dependsOn: buildJavascript) {
        if (packageJson.contains("test-ci")) {
            args = ['run', 'test-ci']
            inputs.files fileTree(projectDir).matching {
                with excludeDef
            }
        }
    }
    test.dependsOn testJavaScript

    if (packageJson.contains("build-showcase")) {
        task buildShowcase(type: PnpmTask, dependsOn: buildJavascript) {
            environment = ["NODE_OPTIONS": "--max-old-space-size=4096"]
            args = ['run', 'build-showcase']
            doLast {
                copy {
                    from packageJson.contains("\"name\": \"tailwind-components\"") ? ".output/public" : "showCase"
                    into "${parent.buildDir}/generated/main/resources/public_html/apps/" + project.name
                }
            }
            outputs.dir packageJson.contains("\"name\": \"tailwind-components\"") ? ".output/public" : "showCase"
            //for caching
            inputs.files fileTree(projectDir).matching {
                with excludeDef
            }
        }
        parent.tasks.processResources.dependsOn buildShowcase
    }
}

sourceSets {
    main {
        resources {
            srcDir "${buildDir}/generated/main/resources"
        }
    }
}

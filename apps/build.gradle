/* will produce a fat jar containing all html/js dist in public_html/apps*/

plugins {
    id "java"
    id "com.github.node-gradle.node" version "7.0.1"
}

def isCI = System.getenv('CI') ? true : false

def nodeSpec = {
    version = '20.11.0'
    yarnVersion = '1.22.19'
    npmVersion = "9.5.1"
    download = !isCI
}

node {
    with nodeSpec
}

task cleanJavascript(type: YarnTask, dependsOn: yarn_install) {
    outputs.upToDateWhen { false }
    args = ['run', 'nx', 'reset']
    delete fileTree(dir: projectDir, include: '**/dist')
}
clean.dependsOn cleanJavascript

task format(type: YarnTask, dependsOn: yarn_install) {
    if (!isCI) {
        args = ['run', 'nx', 'run-many', '--target=format']
        inputs.files fileTree(projectDir).matching {
            // Exclude 'build', 'dist', and 'showCase' directories
            exclude "**${File.separator}build${File.separator}**"
            exclude "**${File.separator}dist${File.separator}**"
            exclude "**${File.separator}showCase${File.separator}**"
            exclude "**${File.separator}node_modules${File.separator}**"
        }
        outputs.files fileTree(projectDir).matching {
            exclude "**${File.separator}build${File.separator}**"
            exclude "**${File.separator}dist${File.separator}**"
            exclude "**${File.separator}node_modules${File.separator}**"
            exclude "**${File.separator}showCase${File.separator}**"
        }
    } else {
        args = ['run', 'nx', 'run-many','--target=checkFormat']
        inputs.files fileTree(projectDir).matching {
            // Exclude 'build', 'dist', and 'showCase' directories
            exclude "**${File.separator}build${File.separator}**"
            exclude "**${File.separator}dist${File.separator}**"
            exclude "**${File.separator}showCase${File.separator}**"
            exclude "**${File.separator}node_modules${File.separator}**"
        }
    }
}

task testJavaScript(type: YarnTask, dependsOn: format) {
    args = ['run', 'nx', 'run-many', '--target=test-ci']
    inputs.files fileTree(projectDir).matching {
        // Exclude 'build', 'dist', and 'showCase' directories
        exclude "**${File.separator}build${File.separator}**"
        exclude "**${File.separator}dist${File.separator}**"
        exclude "**${File.separator}showCase${File.separator}**"
        exclude "**${File.separator}node_modules${File.separator}**"
    }
}
test.dependsOn testJavaScript

task buildJavascript(type: YarnTask, dependsOn: format) {
    environment = ["NODE_OPTIONS": "--max-old-space-size=4096"]
    args = ['run', 'nx', 'run-many', '--target=build,build-showcase', "--parallel=${Math.ceil(Runtime.runtime.availableProcessors() / 2)}", '--verbose']
    doLast {
        file("${project.projectDir}").eachDir { dir ->
            if (dir.name == 'molgenis-components' || dir.name == 'molgenis-viz') {
                copy {
                    from dir.path + "/showCase"
                    into "${buildDir}/generated/main/resources/public_html/apps/" + dir.name
                }
            } else if (dir.name != ".gradle" && dir.name != 'build' && dir.name != 'node_modules') {
                println dir.path + "/dist"
                copy {
                    from dir.name + "/dist"
                    into "${buildDir}/generated/main/resources/public_html/apps/" + dir.name
                }
            }
        }
    }
    inputs.files fileTree(projectDir).matching {
        // Exclude 'build', 'dist', and 'showCase' directories
        exclude "**${File.separator}build${File.separator}**"
        exclude "**${File.separator}dist${File.separator}**"
        exclude "**${File.separator}node_modules${File.separator}**"
        exclude "**${File.separator}showCase${File.separator}**"
    }
    outputs.dir(file("**${File.separator}dist"))
    outputs.dir(file("**${File.separator}showCase"))
}

processResources.dependsOn buildJavascript

sourceSets {
    main {
        resources {
            srcDir "${buildDir}/generated/main/resources"
        }
    }
}

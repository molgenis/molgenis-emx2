plugins {
    id "base"
}

subprojects {
    task buildPython(type:Exec) {
        if(version.toString().contains("SNAPSHOT")) {
            //must comply with https://peps.python.org/pep-0440/
            //todo: can we somehow include the github hash?
            new File(projectDir, "version.txt").text = version.replace("-SNAPSHOT","").replace("v","") + ".dev" + new Date().getTime()
        } else {
            new File(projectDir, "version.txt").text = version.replace("v","")
        }
        group 'build'
        description 'run python lib build'
        //convert version number to pep440 scheme
        commandLine'python3', '-m', 'build'
        ext.output = {
            return standardOutput.toString()
        }
        //for caching (so our build is not slowed by having python build all the time)
        // ... a bit messy is python
        outputs.dir('dist')
        outputs.dir(project.name + ".egg-info")
        outputs.files fileTree('src').matching {
            include '**/.egg-info/*'
        }
        inputs.files fileTree('src').matching {
            exclude '**/.egg-info/*'
        }
        inputs.file('pyproject.toml')
        inputs.file('requirements.txt')
        inputs.file('README.md')
        doLast {
            println "remove version.txt again"
            delete "version.txt"
        }
    }

    task publishPython(type:Exec) {
        dependsOn buildPython
        group 'release'
        description 'publish python package'
        //expects credentials for testpypi
        if(version.toString().contains("SNAPSHOT")) {
            commandLine 'twine', 'upload', '--repository', 'testpypi', 'dist/*'
        } else  {
            //todo switch to production when stable
            commandLine 'twine', 'upload', 'dist/*'
        }
        ext.output = {
            return standardOutput.toString()
        }
        inputs.dir('dist')
    }

    task clean {
        doLast {
            delete 'dist'
            //maybe too much, but want to get rid of all these .egg-info
            delete project.name + ".egg-info"
            def directoryList = []
            new File("$projectDir/src").traverse(filter: ~/.*.egg-info/, type: groovy.io.FileType.DIRECTORIES) {
                directoryList << it
            }
            //need to first list then delete otherwise error
            directoryList.each { dirObj ->
                if (dirObj.exists()) {
                    println "Removing directory: $dirObj"
                    dirObj.deleteDir()
                }
            }
        }
    }
}


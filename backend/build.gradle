plugins {
    id "jacoco"
    id "java"
    id "com.diffplug.spotless" version "6.25.0" apply false
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: "com.diffplug.spotless"

    repositories {
        mavenLocal()
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
    }

    spotless {
        java {
            googleJavaFormat('1.15.0')
        }
    }
    compileJava.dependsOn 'spotlessApply'

    task format {
        dependsOn spotlessApply
    }

    /* default dependencies for all projects*/
    dependencies {
        implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
        implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0'
        implementation 'org.jooq:jooq:3.18.7'
        implementation 'com.zaxxer:HikariCP:5.0.1'
        implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.16.1'
        implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.16.1'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
        implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.1'
        implementation 'com.graphql-java:graphql-java:21.1'
        implementation 'com.graphql-java:graphql-java-extended-scalars:20.2'
        implementation 'com.sparkjava:spark-core:2.9.4'
        implementation 'org.pac4j:pac4j-core:5.4.5'
        implementation 'org.pac4j:pac4j-oidc:5.4.5'
        implementation 'org.pac4j:pac4j-http:5.4.5'
        implementation 'org.pac4j:spark-pac4j:5.0.0'
        implementation 'org.javers:javers-core:6.14.0'
        implementation 'com.nimbusds:nimbus-jose-jwt:9.37'
        implementation 'com.schibsted.spt.data:jslt:0.1.14'

        //also used outside test
        implementation 'org.junit.jupiter:junit-jupiter:5.10.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
        testImplementation 'org.mockito:mockito-core:5.6.0'
        //allows to set environment variables in test
        testImplementation 'com.github.stefanbirkner:system-lambda:1.2.1'
    }

    jacoco {
        toolVersion = "0.8.11"
    }
    jacocoTestReport {
        reports {
            xml.required = true
            csv.required = false
            html.required = false
        }
    }

    targetCompatibility = '17'
    sourceCompatibility = '17'

    jar {
        reproducibleFileOrder = true
        manifest {
            attributes(
                    'Specification-Version': project.version.toString(),
                    'Implementation-Version': getGitHash(),
                    'Created-By': "Gradle ${gradle.gradleVersion}",
                    'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                    'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
            )
        }
    }

    test {
        jvmArgs("--add-opens=java.base/java.util=ALL-UNNAMED", "--add-opens=java.base/java.lang=ALL-UNNAMED")
        useJUnitPlatform()
        failFast true
        testLogging {
            events "passed", "skipped", "failed" //, "standardOut", "standardError"

            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            showStandardStreams = false
        }
    }
    test.dependsOn ":backend:initDatabase"

    tasks.register('testFast', Test) {
        jvmArgs("--add-opens=java.base/java.util=ALL-UNNAMED")
        useJUnitPlatform {
            excludeTags "slow"
        }
        failFast true
        testLogging {
            events "passed", "skipped", "failed" //, "standardOut", "standardError"

            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            showStandardStreams = false
        }
    }
    tasks.register('testWindows', Test) {
        jvmArgs("--add-opens=java.base/java.util=ALL-UNNAMED")
        useJUnitPlatform {
            excludeTags "windowsFail"
        }
        failFast true
        testLogging {
            events "passed", "skipped", "failed" //, "standardOut", "standardError"

            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            showStandardStreams = false
        }
    }
}

/* have the non-parallel test package run last*/
project(":backend:molgenis-emx2-nonparallel-tests").test.dependsOn subprojects.findAll { it != project(":backend:molgenis-emx2-nonparallel-tests") }.collect { ":backend:${it.name}:test" }

/* task to init database */
task initDatabase(type: JavaExec, dependsOn: ':backend:molgenis-emx2-sql:compileJava') {
    main = 'org.molgenis.emx2.sql.InitDatabase'
    classpath = project("molgenis-emx2-sql").sourceSets.test.runtimeClasspath
    def outputFile = file("$buildDir/initDatabase.txt")
    doLast {
        //for caching purposes
        outputFile.text = "This is the output of the runMainClass task."
    }
    outputs.file(outputFile)
    outputs.upToDateWhen { outputFile.exists() }
}

/* generate java test coverage report*/
test.dependsOn subprojects.test
task jacocoMergedReport(type: JacocoReport) {
    dependsOn test
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
}

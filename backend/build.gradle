plugins {
    id "jacoco"
    id "java"
    id "com.diffplug.spotless" version "6.25.0" apply false
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: "com.diffplug.spotless"

    repositories {
        mavenLocal()
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
    }

    spotless {
        java {
            googleJavaFormat('1.22.0')
        }
    }
    compileJava.dependsOn 'spotlessApply'

    task format {
        dependsOn spotlessApply
    }

    /* default dependencies for all projects*/
    dependencies {
        testImplementation group: 'org.junit.jupiter', name:'junit-jupiter-api', version:junitVersion
        testRuntimeOnly group: 'org.junit.jupiter', name:'junit-jupiter-engine', version:junitVersion
        testImplementation group: 'org.mockito', name:'mockito-core', version:mockitoVersion
        //allows to set environment variables in test
        testImplementation group: 'com.github.stefanbirkner', name:'system-lambda', version:systemLambdaVersion
    }

    jacoco {
        toolVersion = "0.8.12"
    }
    jacocoTestReport {
        reports {
            xml.required = true
            csv.required = false
            html.required = false
        }
    }

    targetCompatibility = rootProject.ext.javaVersion
    sourceCompatibility = rootProject.ext.javaVersion

    jar {
        reproducibleFileOrder = true
        manifest {
            attributes(
                    'Specification-Version': project.version.toString(),
                    'Implementation-Version': getGitHash(),
                    'Created-By': "Gradle ${gradle.gradleVersion}",
                    'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                    'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
            )
        }
    }

    test {
        jvmArgs("--add-opens=java.base/java.util=ALL-UNNAMED", "--add-opens=java.base/java.lang=ALL-UNNAMED")
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed" //, "standardOut", "standardError"

            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            showStandardStreams = false
        }
    }
    test.dependsOn ":backend:initDatabase"

    tasks.register('testFast', Test) {
        jvmArgs("--add-opens=java.base/java.util=ALL-UNNAMED")
        useJUnitPlatform {
            excludeTags "slow"
        }
        failFast true
        testLogging {
            events "passed", "skipped", "failed" //, "standardOut", "standardError"

            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            showStandardStreams = false
        }
    }
    tasks.register('testWindows', Test) {
        jvmArgs("--add-opens=java.base/java.util=ALL-UNNAMED")
        useJUnitPlatform {
            excludeTags "windowsFail"
        }
        failFast true
        testLogging {
            events "passed", "skipped", "failed" //, "standardOut", "standardError"

            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            showStandardStreams = false
        }
    }
}

/* have the non-parallel test package run last*/
project(":backend:molgenis-emx2-nonparallel-tests").test.dependsOn subprojects.findAll { it != project(":backend:molgenis-emx2-nonparallel-tests") }.collect { ":backend:${it.name}:test" }

/* task to init database */
task initDatabase(type: JavaExec, dependsOn: ':backend:molgenis-emx2-sql:compileJava') {
    main = 'org.molgenis.emx2.sql.InitDatabase'
    classpath = project("molgenis-emx2-sql").sourceSets.test.runtimeClasspath
    def outputFile = file("$buildDir/initDatabase.txt")
    doLast {
        //for caching purposes
        outputFile.text = "This is the output of the runMainClass task."
    }
    outputs.file(outputFile)
    outputs.upToDateWhen { outputFile.exists() }
}

/* generate java test coverage report*/
test.dependsOn subprojects.test
task jacocoMergedReport(type: JacocoReport) {
    dependsOn test
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
}
